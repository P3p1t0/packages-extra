pkgbase=bootsplash-themes
pkgname=('bootsplash-theme-tux' 'bootsplash-theme-manjaro-elegant')
pkgver=0.1.1
pkgrel=2
url="https://lists.freedesktop.org/archives/dri-devel/2017-December/160242.html"
arch=('x86_64')
license=('GPL')

depends=('linux>=4.14.6' 'systemd')
builddepends=('imagemagick')
options=('!libtool' '!emptydirs')

source=('bootsplash-packer'
	'bootsplash-tux.sh'
	'bootsplash-tux.initcpio_install'
	'bootsplash-manjaro-elegant.sh'
	'bootsplash-manjaro-elegant.initcpio_install'
	'ajax-loader.gif'
	'tux.gif'
	'manjaro-elegant.gif')

sha256sums=('51559d3ccfb448b03fa6439faf5869dbd0c2fbda1b5d5bf5d4ba70e60937472a'
            '5b634d8448f6f1f80c47d05a176682d35ae386dfc9e4f3c4217d80113d55802a'
            'bcc1da803dfef9edd32f9fb245593c67de500437a02a1d266e3e63af64d8ef4f'
            '7a5aed60818fe0629cbc9956901bfc360add1d0e609821ab7331e0e66c05c918'
            'e8deebc1e8350a22daa6abb2c2d5a9cfbf3f28785d12eea77dce9fc8da1097fc'
            'aebc793d0064383ee6b1625bf3bb32532ec30a5c12bf9117066107d412119123'
            '4cdf8d34e001fc7f15b61823eee5617f5389e153d7d317471d0f9d982c0a2745'
            '91c0d24b3973a49286c250c185cccf1c5b1da31d89a2ecf6e0e65bc65e561d35')

build() {
  cd "$srcdir"
  sh ./bootsplash-tux.sh
  sh ./bootsplash-manjaro-elegant.sh
}

package_bootsplash-theme-tux() {
  pkgdesc="Bootsplash Theme Tux"
  cd "$srcdir"

  install -Dm644 "$srcdir/bootsplash-tux" "$pkgdir/usr/lib/firmware/bootsplash-themes/tux/bootsplash"
  install -Dm644 "$srcdir/bootsplash-tux.initcpio_install" "$pkgdir/usr/lib/initcpio/install/bootsplash-tux"
}

package_bootsplash-theme-manjaro-elegant() {
  pkgdesc="Bootsplash Theme 'Manjaro elegant'"
  cd "$srcdir"

  install -Dm644 "$srcdir/bootsplash-manjaro-elegant" "$pkgdir/usr/lib/firmware/bootsplash-themes/manjaro-elegant/bootsplash"
  install -Dm644 "$srcdir/bootsplash-manjaro-elegant.initcpio_install" "$pkgdir/usr/lib/initcpio/install/bootsplash-manjaro-elegant"
}
